<!DOCTYPE html>

<html>
    <head>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, initial-scale=1.0">

        <link rel="stylesheet" type="text/css" href="style.css">

        <link rel="stylesheet" href="thirdparty/skeleton/css/normalize.css">
        <link rel="stylesheet" href="thirdparty/skeleton/css/skeleton.css">

        <link rel="stylesheet" type="text/css" href="viewer.css">

        <title>Centria Seminaari</title>

    </head>
    
    <body>
        <div class="main">
            <div id="side-panel" class="panel">
                <div class="row">
                    <h2 id="viewer-active-panel-name"></h2>
                    <button id="viewer-hierarchy-panel-button">Hierarchy</button>
                    <button id="viewer-render-panel-button">Settings</button>
                </div>

                <div id="panel-inner">
                </div>
            </div>
            <div id="scene-view" class="panel">
                <canvas id="canvas"></canvas>
            </div>
            <div id="fps-counter">
                0
            </div>
        </div>

		<script src="engine/math/color.js"></script>
		<script src="engine/math/mat3.js"></script>
		<script src="engine/math/mat4.js"></script>
		<script src="engine/math/quaternion.js"></script>
		<script src="engine/math/vec3.js"></script>

		<script src="engine/core/core.js"></script>

		<script src="engine/core/resource.js"></script>
		<script src="engine/core/component.js"></script>
		<script src="engine/graphics/renderable.js"></script>

		<script src="engine/core/file.js"></script>
		<script src="engine/core/filehandler.js"></script>
		<script src="engine/core/inputmanager.js"></script>
		<script src="engine/core/resources.js"></script>
		<script src="engine/core/serialize.js"></script>

		<script src="engine/core/events.js"></script>
        
		<script src="engine/graphics/camera.js"></script>
		<script src="engine/graphics/cameraController.js"></script>
		<script src="engine/graphics/cubemap.js"></script>
		<script src="engine/graphics/debug.js"></script>
		<script src="engine/graphics/environmentmap.js"></script>
		<script src="engine/graphics/geometry.js"></script>
		<script src="engine/graphics/light.js"></script>
		<script src="engine/graphics/material.js"></script>
		<script src="engine/graphics/mesh.js"></script>
		<script src="engine/graphics/model.js"></script>
		<script src="engine/graphics/models.js"></script>
		<script src="engine/graphics/normalrenderer.js"></script>
		<script src="engine/graphics/renderer.js"></script>
		<script src="engine/graphics/renderview.js"></script>
		<script src="engine/graphics/scene.js"></script>
		<script src="engine/graphics/shader.js"></script>
		<script src="engine/graphics/texture.js"></script>

		<script src="viewer/main.js"></script>
		<script src="viewer/renderPanel.js"></script>
		<script src="viewer/hierarchyPanel.js"></script>
		<script src="viewer/viewer.js"></script>

        <script>
            window.addEventListener("load", function() {
                viewerMain();
            }, false);

        </script>

        <script id="default-shader" type="noscript">
        precision mediump float;

        #ifdef COMPILE_FRAGMENT
            #define varying in
        #endif

        #if defined(DIFFUSEMAP) || defined(SPECMAP) || defined(AMBIENTMAP) || defined(NORMALMAP)
            #define TEXCOORDS
        #endif

        #if defined(EMISSIONMAP) && !defined(TEXCOORDS)
            #define TEXCOORDS
        #endif

        #if defined(NORMALMAP)
            #define TANGENTS
        #endif

        #if defined(DIFFUSEMAP) || defined(SPECMAP) || defined(AMBIENTMAP) || defined(NORMALMAP)
            #define TEXCOORDS
        #endif

        #if defined(EMISSIONMAP) && !defined(TEXCOORDS)
            #define TEXCOORDS
        #endif

        #if defined(NORMALMAP)
            #define TANGENTS
        #endif

        #ifdef COMPILE_VERTEX
            #define varying out

            uniform mat4 uModelViewMatrix;
            uniform mat4 uModelMatrix;
            uniform mat4 uProjectionMatrix;

            uniform mat4 uViewProjectionMatrix;
            uniform mat4 uViewMatrix;

            in vec3 iPosition;

            #ifdef VERTEX_COLORS
                in vec3 iColor;
            #endif

            #ifdef NORMALS
                in vec3 iNormal;
            #endif

            #ifdef TEXCOORDS
                in vec2 iTexCoord;
            #endif

            #ifdef NORMALMAP
                in vec3 iTangent;
                in vec3 iBitangent;
            #endif

            #ifdef INSTANCING
                in mat4 iInstanceModelMatrix;
            #endif

            #ifdef ENVIRONMENTMAP
                uniform vec3 uViewPosition;
            #endif

        #endif

        #define TEXTURE_LOD_BIAS -1.0

        #if __VERSION__ >= 130
            #define texture2D(sampler, uv) texture(sampler, uv, TEXTURE_LOD_BIAS)
        #endif

        #ifdef VERTEX_COLORS
            varying vec3 vColor;
        #endif

        #ifdef TEXCOORDS
            varying vec2 vTexCoord;
        #endif

        #ifdef NORMALS
            varying vec3 vNormal;
        #endif

        #ifdef NORMALMAP
            varying vec3 vTangent;
            varying vec3 vBitangent;
        #endif

        #ifdef ENVIRONMENTMAP
            varying vec3 vReflection;
        #endif

        varying vec3 vWorldPos;

        ///////////////////////////////////////////////////////////////////////////////
        // Vertex shader

        #ifdef COMPILE_VERTEX

        void main()
        {
            #ifdef INSTANCING
                vec4 pos = iInstanceModelMatrix * vec4(iPosition, 1.0);
                vWorldPos = pos.xyz;

                pos = uViewProjectionMatrix * pos;

                #ifdef NORMALS
                    vec3 normal = normalize((mat3(iInstanceModelMatrix) * iNormal));
                #endif

            #else
                vec4 pos = uModelViewMatrix * vec4(iPosition, 1.0);
                pos = uProjectionMatrix * pos;
                vWorldPos = (uModelMatrix * vec4(iPosition, 1.0)).xyz;

                #ifdef NORMALS
                    vec3 normal = normalize((mat3(uModelMatrix)) * iNormal);
                #endif
            #endif

            #ifdef NORMALS
                vNormal = normal;
            #endif

            #ifdef VERTEX_COLORS
                vColor = iColor;
            #endif

            #ifdef TEXCOORDS
                vTexCoord = iTexCoord;
            #endif

            #ifdef NORMALMAP
                vTangent = iTangent;
                vBitangent = iBitangent;
            #endif

            #ifdef ENVIRONMENTMAP
                vReflection = vWorldPos - uViewPosition;
            #endif

            gl_Position = pos;
        }

        #endif

        #ifdef COMPILE_FRAGMENT

        struct Light {
            vec3 position;
            vec4 color;
            float range;
        };


        uniform sampler2D sDiffuseMap;
        uniform sampler2D sSpecularMap;
        uniform sampler2D sAmbientMap;
        uniform sampler2D sNormalMap;
        uniform sampler2D sHeightMap;
        // uniform sampler2D sEmissionMap;
        uniform samplerCube sEnvironmentMap;

        uniform vec4 uDiffuseColor;
        uniform vec4 uSpecularColor;
        uniform vec4 uAmbientColor;
        uniform vec3 uEmission;

        #ifdef LIGHTS
        uniform Light uLights[MAX_LIGHTS];
        uniform vec3 uViewForward;
        uniform vec3 uViewPosition;
        #endif

        #ifdef FOG

        // uFogParams.x: fogStart
        // uFogParams.y: fogEnd
        uniform vec2 uFogParams;
        uniform vec3 uFogColor;

        #endif

        out vec4 fColor;

        #ifdef LIGHTS

        vec4 GetLighting(vec3 normal, vec4 diffColor, vec4 specColor)
        {
            vec4 lighting = vec4(0.0, 0.0, 0.0, 1.0);

            for (int i = 0; i < MAX_LIGHTS; ++i) {
                vec3 surfaceToLightDelta = uLights[i].position - vWorldPos;
                vec3 surfaceToLight = normalize(surfaceToLightDelta); 
                vec3 surfaceToView = normalize(uViewPosition - vWorldPos);
                
                vec3 halfVector = normalize(surfaceToLight + surfaceToView);

                float diffFactor = max(dot(normal, surfaceToLight), 0.0);
                float specFactor = 0.0;
                if (diffFactor > 0.0)
                    specFactor = pow(max(dot(normal, halfVector), 0.0), specColor.a);

                float atten = clamp(1.0 - uLights[i].range * 2.0 / length(surfaceToLightDelta), 0.0, 1.0);
                atten = pow(1.0 - atten, 2.0);

                vec3 amount = (diffColor.rgb * uLights[i].color.rgb * diffFactor +
                                specColor.rgb * specFactor * atten) * uLights[i].color.a * atten;

                lighting.rgb += amount;

            }

            return lighting;
        }

        #endif

        #ifdef FOG
        float GetFogFactor(float pos)
        {
            float start = uFogParams.x;
            float end = uFogParams.y;

            return 1.0 - clamp(((end - pos) / (end - start)), 0.0, 1.0);
        }
        #endif

        #ifdef NORMALMAP
        vec3 UnpackNormal(vec3 packed) {
            return normalize(packed * 2.0 - 1.0);
        }
        #endif

        ///////////////////////////////////////////////////////////////////////////////
        // Fragment shader

        void main()
        {
            vec4 diff = uDiffuseColor;

            #ifdef DIFFUSEMAP
                diff *= texture2D(sDiffuseMap, vTexCoord);
            #endif

            #ifdef VERTEX_COLORS
                diff.rgb *= vColor;
            #endif

            #ifdef LIGHTS
                vec4 spec = uSpecularColor;

                #ifdef SPECMAP
                    spec.rgb *= texture2D(sSpecularMap, vTexCoord).rgb;
                #endif

                #ifdef NORMALMAP
                    vec3 normal = UnpackNormal(texture2D(sNormalMap, vTexCoord).xyz);
                    mat3 tbnMatrix = mat3(vTangent, vBitangent, vNormal);

                    normal = normalize(tbnMatrix * normal);
                #else
                    vec3 normal = normalize(vNormal);
                #endif

                vec4 col = vec4(GetLighting(normal, diff, spec).rgb, diff.a);
            #else
                vec4 col = diff;
            #endif

            #ifdef AMBIENTMAP
                float ambientIntensity = texture2D(sAmbientMap, vTexCoord).r;
                col.rgb += diff.rgb * uAmbientColor.rgb * ambientIntensity;
            #else
                col.rgb += diff.rgb * uAmbientColor.rgb;
            #endif

            #ifdef EMSSIONMAP
                col.rgb += col.rgb * uEmissionMap.r;
            #endif

            #ifdef ENVIRONMENTMAP
                #ifdef SPECMAP
                    col.rgb += texture(sEnvironmentMap, normalize(reflect(vReflection, normal)), 5.0).rgb *
                        texture2D(sSpecularMap, vTexCoord).rgb * 0.5f;
                #else
                    col.rgb += texture(sEnvironmentMap, normalize(reflect(vReflection, normal)), -20.0).rgb * 0.25;
                #endif
            #endif

            #ifdef FOG
                col.rgb = mix(col.rgb, uFogColor, GetFogFactor(gl_FragCoord.z / gl_FragCoord.w));
            #endif

            fColor = col;

            #ifdef NORMALMAP
            // fColor.rgb = texture2D(sNormalMap, vTexCoord).xyz;
            #endif
        }

        #endif
        </script>

        <script id="debug-shader" type="noscript">
        #ifdef COMPILE_FRAGMENT
            #define varying in
            
            precision mediump float;
        #endif

        #ifdef COMPILE_VERTEX
            #define varying out

            uniform mat4 uViewProjectionMatrix;

            in vec3 iPosition;
            in vec4 iColor;
        #endif

        varying vec4 vColor;

        #ifdef COMPILE_VERTEX

        uniform vec4 uTint;

        ///////////////////////////////////////////////////////////////////////////////
        // Vertex shader

        void main() {
            vColor = iColor * uTint;
            gl_Position = uViewProjectionMatrix * vec4(iPosition, 1.0);
        }

        #endif

        #ifdef COMPILE_FRAGMENT

        out vec4 fColor;

        ///////////////////////////////////////////////////////////////////////////////
        // Fragment shader

        void main() {
            fColor = vColor;
        }

        #endif

        </script>

    </body>
</html>
